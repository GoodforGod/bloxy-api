buildscript {
    ext.kotlin_version = '1.3.10'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group = 'com.github.goodforgod'
version = '1.0.3'

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    api group: 'org.jetbrains', name: 'annotations', version: '13.0'
    api group: 'com.beust', name: 'klaxon', version: '5.0.4'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Bloxy API'
                url = 'https://github.com/GoodforGod/bloxy-api'
                description = 'Kotlin & Java Library for all available Bloxy.info API endpoints'

                license {
                    name = 'MIT License'
                    url = 'https://github.com/GoodforGod/bloxy-api/blob/master/LICENSE'
                    distribution = 'repo'
                }

                developer {
                    id = 'GoodforGod'
                    name = 'Anton Kurako'
                    email = 'goodforgod.dev@gmail.com'
                    url = 'https://github.com/GoodforGod'
                }

                scm {
                    connection = 'scm:git:git://github.com/GoodforGod/bloxy-api.git'
                    developerConnection = 'scm:git:ssh://GoodforGod/bloxy-api.git'
                    url = 'https://github.com/GoodforGod/bloxy-api/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.properties['ossrhUsername']
                password project.properties['ossrhPassword']
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
}
